FUNCTION_BLOCK computerEvaluation

   VAR_INPUT
      cpuSpeed: REAL;
      threadNumber: REAL;
      cores: REAL;
      storageSize: REAL;
      ram: REAL;
      gpuDedicatedMemory: REAL;
   END_VAR

   VAR_OUTPUT
      homeUseSuitability: REAL;
      businessUseSuitability: REAL;
      gamingSuitability: REAL;
      miningSuitability: REAL;
      hostingSuitability: REAL;
   END_VAR

   // Фазификација CPU брзине
   FUZZIFY cpuSpeed
      TERM slow := (0, 0) (2, 1) (3, 0);
      TERM medium := (2, 0) (3, 1) (4, 0);
      TERM fast := (3, 0) (4, 1) (5, 0);
      TERM veryFast := (4, 0) (5, 1) (6, 0);
   END_FUZZIFY

   // Фазификација броја тредова
   FUZZIFY threadNumber
      TERM few := (0, 0) (4, 1) (8, 0);
      TERM average := (4, 0) (8, 1) (16, 0);
      TERM many := (8, 0) (16, 1);
      TERM veryMany := (12, 0) (16, 1);
   END_FUZZIFY

   // Фазификација броја језгара
   FUZZIFY cores
      TERM few := (0, 0) (4, 1) (8, 0);
      TERM average := (4, 0) (8, 1) (16, 0);
      TERM many := (8, 0) (16, 1);
   END_FUZZIFY

   // Фазификација величине складишта
   FUZZIFY storageSize
      TERM small := (0, 0) (256, 1) (512, 0);
      TERM medium := (256, 0) (512, 1) (1024, 0);
      TERM large := (512, 0) (1024, 1);
   END_FUZZIFY

   // Фазификација величине RAM меморије
   FUZZIFY ram
      TERM veryLow := (0, 0) (4, 1) (8, 0);
      TERM low := (4, 0) (8, 1) (16, 0);
      TERM medium := (8, 0) (16, 1) (32, 0);
      TERM high := (16, 0) (32, 1) (64, 0);
      TERM veryHigh := (32, 0) (64, 1);
   END_FUZZIFY

   // Фазификација дедиковане меморије GPU-а
   FUZZIFY gpuDedicatedMemory
      TERM veryLow := (0, 0) (2, 1) (4, 0);
      TERM low := (2, 0) (4, 1) (6, 0);
      TERM medium := (4, 0) (6, 1) (8, 0);
      TERM high := (6, 0) (8, 1) (10, 0);
      TERM veryHigh := (8, 0) (10, 1);
   END_FUZZIFY

   // Дефазификација за сврху кућне употребе
   DEFUZZIFY homeUseSuitability
      TERM low := (0, 0) (0.5, 1) (1, 0);
      TERM medium := (0.5, 0) (0.75, 1) (1, 0);
      TERM high := (0.75, 0) (1, 1);
      METHOD : COG;
      DEFAULT := 0;
      RANGE := (0 .. 1);
   END_DEFUZZIFY

   // Дефазификација за сврху пословне употребе
   DEFUZZIFY businessUseSuitability
      TERM low := (0, 0) (0.5, 1) (1, 0);
      TERM medium := (0.5, 0) (0.75, 1) (1, 0);
      TERM high := (0.75, 0) (1, 1);
      METHOD : COG;
      DEFAULT := 0;
      RANGE := (0 .. 1);
   END_DEFUZZIFY

   // Дефазификација за сврху играња
   DEFUZZIFY gamingSuitability
      TERM low := (0, 0) (0.5, 1) (1, 0);
      TERM medium := (0.5, 0) (0.75, 1) (1, 0);
      TERM high := (0.75, 0) (1, 1);
      METHOD : COG;
      DEFAULT := 0;
      RANGE := (0 .. 1);
   END_DEFUZZIFY

   // Дефазификација за сврху рударења
   DEFUZZIFY miningSuitability
      TERM low := (0, 0) (0.5, 1) (1, 0);
      TERM medium := (0.5, 0) (0.75, 1) (1, 0);
      TERM high := (0.75, 0) (1, 1);
      METHOD : COG;
      DEFAULT := 0;
      RANGE := (0 .. 1);
   END_DEFUZZIFY

   // Дефазификација за сврху хостинга
   DEFUZZIFY hostingSuitability
      TERM low := (0, 0) (0.5, 1) (1, 0);
      TERM medium := (0.5, 0) (0.75, 1) (1, 0);
      TERM high := (0.75, 0) (1, 1);
      METHOD : COG;
      DEFAULT := 0;
      RANGE := (0 .. 1);
   END_DEFUZZIFY

   RULEBLOCK No1
      // Правила за кућну употребу
      RULE 1 : IF cores IS few AND ram IS low AND gpuDedicatedMemory IS low THEN homeUseSuitability IS low;
      RULE 2 : IF cores IS average AND ram IS medium THEN homeUseSuitability IS medium;
      RULE 3 : IF cores IS many THEN homeUseSuitability IS high;
      RULE 4 : IF gpuDedicatedMemory IS high AND ram IS medium THEN homeUseSuitability IS high;
      RULE 5 : IF ram IS low AND gpuDedicatedMemory IS low THEN homeUseSuitability IS low;
      RULE 6 : IF cpuSpeed IS slow AND ram IS low THEN homeUseSuitability IS low;
      RULE 7 : IF cpuSpeed IS fast AND ram IS high THEN homeUseSuitability IS high;
      RULE 8 : IF threadNumber IS few THEN homeUseSuitability IS low;
      RULE 9 : IF gpuDedicatedMemory IS veryHigh THEN homeUseSuitability IS high;

      // Правила за пословну употребу
      RULE 10 : IF cores IS average AND gpuDedicatedMemory IS medium THEN businessUseSuitability IS medium;
      RULE 11 : IF cores IS many AND ram IS high THEN businessUseSuitability IS high;
      RULE 12 : IF cores IS few THEN businessUseSuitability IS low;
      RULE 13 : IF ram IS medium AND gpuDedicatedMemory IS medium THEN businessUseSuitability IS medium;
      RULE 14 : IF gpuDedicatedMemory IS low THEN businessUseSuitability IS low;
      RULE 15 : IF threadNumber IS many THEN businessUseSuitability IS high;
      RULE 16 : IF storageSize IS small THEN businessUseSuitability IS low;
      RULE 17 : IF storageSize IS large THEN businessUseSuitability IS high;

      // Правила за играње
      RULE 18 : IF cores IS many AND gpuDedicatedMemory IS high AND ram IS high THEN gamingSuitability IS high;
      RULE 19 : IF cores IS average AND gpuDedicatedMemory IS medium THEN gamingSuitability IS medium;
      RULE 20 : IF cores IS few THEN gamingSuitability IS low;
      RULE 21 : IF ram IS medium AND gpuDedicatedMemory IS medium THEN gamingSuitability IS medium;
      RULE 22 : IF gpuDedicatedMemory IS high AND ram IS medium THEN gamingSuitability IS high;
      RULE 23 : IF cpuSpeed IS fast AND gpuDedicatedMemory IS high THEN gamingSuitability IS high;
      RULE 24 : IF storageSize IS large AND ram IS high THEN gamingSuitability IS high;
      RULE 25 : IF storageSize IS small THEN gamingSuitability IS low;
      RULE 26 : IF gpuDedicatedMemory IS veryHigh THEN gamingSuitability IS high;

      // Правила за рударење
      RULE 27 : IF gpuDedicatedMemory IS high AND cores IS many THEN miningSuitability IS high;
      RULE 28 : IF gpuDedicatedMemory IS medium AND cores IS average THEN miningSuitability IS medium;
      RULE 29 : IF cores IS few AND gpuDedicatedMemory IS low THEN miningSuitability IS low;
      RULE 30 : IF ram IS high AND gpuDedicatedMemory IS high THEN miningSuitability IS high;
      RULE 31 : IF ram IS low AND gpuDedicatedMemory IS low THEN miningSuitability IS low;
      RULE 32 : IF cpuSpeed IS slow THEN miningSuitability IS low;
      RULE 33 : IF threadNumber IS many AND gpuDedicatedMemory IS high THEN miningSuitability IS high;
      RULE 34 : IF gpuDedicatedMemory IS veryHigh THEN miningSuitability IS high;

      // Правила за хостинг
      RULE 35 : IF cores IS average AND gpuDedicatedMemory IS medium THEN hostingSuitability IS medium;
      RULE 36 : IF cores IS many AND ram IS high THEN hostingSuitability IS high;
      RULE 37 : IF cores IS few THEN hostingSuitability IS low;
      RULE 38 : IF ram IS medium AND gpuDedicatedMemory IS medium THEN hostingSuitability IS medium;
      RULE 39 : IF ram IS high AND gpuDedicatedMemory IS high THEN hostingSuitability IS high;
      RULE 40 : IF storageSize IS large AND cores IS many THEN hostingSuitability IS high;
      RULE 41 : IF gpuDedicatedMemory IS veryHigh THEN hostingSuitability IS high;

      // Додатна правила
      RULE 42 : IF cpuSpeed IS fast AND ram IS high AND gpuDedicatedMemory IS high THEN gamingSuitability IS high;
      RULE 43 : IF threadNumber IS few AND cores IS few THEN gamingSuitability IS low;
      RULE 44 : IF cpuSpeed IS slow AND ram IS low THEN gamingSuitability IS low;
      RULE 45 : IF threadNumber IS many AND storageSize IS large THEN businessUseSuitability IS high;
      RULE 46 : IF cpuSpeed IS medium AND gpuDedicatedMemory IS medium THEN miningSuitability IS medium;
      RULE 47 : IF storageSize IS large AND ram IS high THEN hostingSuitability IS high;
      RULE 48 : IF ram IS low AND storageSize IS small THEN hostingSuitability IS low;
      RULE 49 : IF cores IS average AND threadNumber IS few THEN miningSuitability IS medium;
      RULE 50 : IF gpuDedicatedMemory IS veryHigh AND cpuSpeed IS veryFast THEN gamingSuitability IS high;
      RULE 51 : IF ram IS low AND gpuDedicatedMemory IS low THEN gamingSuitability IS low;

   END_RULEBLOCK

END_FUNCTION_BLOCK
